// Получить структуру загрузки.
// 
// Параметры:
//  СтруктураПараметрыЗагрузки - Структура:
// * АдресВоВременномХранилище - Строка
// * ПапкаЗаправок - СправочникСсылка.МестаТранзакции 
// * РеквизитПоискаНоменклатуры - Строка
// * РасширениеФайла - Строка
// 
// Возвращаемое значение:
//  Структура - Получить структуру загрузки:
// * Результат - Булево - 
// * МассивОшибок - Массив - 
// * МассивЗаправкиНеНашли - Массив - 
// * МассивСтрок - Массив из См. ЗагрузкаОбщий.СтруктураСтрокиЭксельДляЗагрузки()  
Функция ПолучитьСтруктуруЗагрузки(СтруктураПараметрыЗагрузки) Экспорт
	СтруктураРезультат = Новый Структура;
	СтруктураРезультат.Вставить("Результат", Истина);
	СтруктураРезультат.Вставить("МассивОшибок", Новый Массив);
	СтруктураРезультат.Вставить("МассивЗаправкиНеНашли", Новый Массив);
	СтруктураРезультат.Вставить("МассивСтрок", Новый Массив);

	ВременныйФайлЭксель = ПолучитьИмяВременногоФайла(СтруктураПараметрыЗагрузки.РасширениеФайла);

	ДвоичныеДанные = ПолучитьИзВременногоХранилища(СтруктураПараметрыЗагрузки.АдресВоВременномХранилище); //ДвоичныеДанные
	ДвоичныеДанные.Записать(ВременныйФайлЭксель);

	Попытка
		Эксель = Новый COMОбъект("Excel.Application");
		Эксель.DisplayAlerts = 0;
		Эксель.Visible = 0;
	Исключение
		СтруктураРезультат.Результат = Ложь;
		СтруктураРезультат.МассивОшибок = ОписаниеОшибки();
		Возврат СтруктураРезультат;
	КонецПопытки;

	СоответствиеКарточек = ЗагрузкаОбщий.СоответствиеКарточек();
	СоответствиеМестТранзакций = ЗагрузкаОбщий.СоответствиеМестТранзакций(СтруктураПараметрыЗагрузки.ПапкаЗаправок);
	СоответствиеНоменклатуры = ЗагрузкаОбщий.СоответсвиеНоменклатуры(
		СтруктураПараметрыЗагрузки.РеквизитПоискаНоменклатуры);

	Эксель.Workbooks.Open(ВременныйФайлЭксель);
	Лист = Эксель.Sheets(1);

	СчетчикПустых = 0;
	МассивКарточек = Новый Массив;
	Для Сч = 5 По 100000 Цикл
		Если СчетчикПустых = 1000 Тогда
			Прервать;
		КонецЕсли;
		ТекущееЗначениеКарты = Лист.Cells(Сч, 3).Value;

		НомерКарточки = СокрЛП(ТекущееЗначениеКарты);
		НомерКарточки = СтрЗаменить(НомерКарточки, Символы.НПП, "");
		Если Не ЗначениеЗаполнено(НомерКарточки) = 1 Тогда
			СчетчикПустых = СчетчикПустых + 1;
			Продолжить;
		Иначе
			СчетчикПустых = 0;
		КонецЕсли;

		ДатаТранзакции    = Лист.Cells(Сч, 1).Value;
		ЭмитентАЗС        = Лист.Cells(Сч, 15).Value;
		ЛокальныйНомерАЗС = Лист.Cells(Сч, 16).Value;

		ЭмитентАЗС = СтрЗаменить(ЭмитентАЗС, Символы.НПП, "");
		ЛокальныйНомерАЗС = СтрЗаменить(ЛокальныйНомерАЗС, Символы.НПП, "");
		
		КодЗаправки = СтрШаблон("%1,%2", СокрЛП(ЭмитентАЗС), СокрЛП(ЛокальныйНомерАЗС));

		ПродуктСтрокой = СокрЛП(Лист.Cells(Сч, 6).Value);

		Если ПродуктСтрокой <> "Оплата дор" Тогда
			КолВо  = Окр(Лист.Cells(Сч, 7).Value, 2);
			Цена  = Окр(Лист.Cells(Сч, 10).Value, 2);
		Иначе
			КолВо  = Окр(Лист.Cells(Сч, 7).Value, 2);
			Цена  = -Окр(Лист.Cells(Сч, 10).Value, 2);
		КонецЕсли;

		ДатаТранзакцииДатой = ДатаТранзакции;

		Если ЗначениеЗаполнено(ДатаТранзакцииДатой) Тогда

			СтруктураСтроки = ЗагрузкаОбщий.СтруктураСтрокиЭксельДляЗагрузки();
			СтруктураСтроки.КартаСтрока = НомерКарточки;
			СтруктураСтроки.Карта = СоответствиеКарточек.Получить(НомерКарточки);
			СтруктураСтроки.ДатаТранзакции = ДатаТранзакцииДатой;
			СтруктураСтроки.Количество = КолВо;
			СтруктураСтроки.ПродуктСтрока = ПродуктСтрокой;
			СтруктураСтроки.Продукт = СоответствиеНоменклатуры.Получить(ПродуктСтрокой);
			СтруктураСтроки.Цена = Цена;
			СтруктураСтроки.Сумма = Цена * КолВо;
			СтруктураСтроки.МестоТранзакцииСтрока = КодЗаправки;
			СтруктураСтроки.МестоТранзакции = СоответствиеМестТранзакций.Получить(КодЗаправки);

			МассивКарточек.Добавить(СтруктураСтроки.Карта);

			СтруктураРезультат.МассивСтрок.Добавить(СтруктураСтроки);

		КонецЕсли;
	КонецЦикла;
	Эксель.ActiveWorkbook.Close(Строка(0));
	Эксель.Quit();

	СоответствиеВладельцевКарт = ЗагрузкаОбщий.СоответствиеВладельцевКарт(МассивКарточек);
	СоответствиеАвтомобилейКарт = ЗагрузкаОбщий.СоответствиеАвтомобилейКарт(МассивКарточек);
	Для Каждого СтруктураМассива Из СтруктураРезультат.МассивСтрок Цикл
		СтруктураМассива.Клиент = СоответствиеВладельцевКарт.Получить(СтруктураМассива.Карта);
		СтруктураМассива.Автомобиль = СоответствиеАвтомобилейКарт.Получить(СтруктураМассива.Карта);
	КонецЦикла;

	Возврат СтруктураРезультат;
КонецФункции