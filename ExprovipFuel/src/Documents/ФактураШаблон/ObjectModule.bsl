
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	СтруктураФактуры = ПолучитьФактуру();
	Если Не СтруктураФактуры.Результат Тогда
		Отказ = Истина;
		//@skip-check undefined-variable
		ОбщегоНазначения.СообщитьПользователю(СтруктураФактуры.Ошибка);
		Возврат;
	КонецЕсли;
	СтруктураНастроек = Справочники.Настройки.ПолучитьНастройки();
	ФактураОбъект = СтруктураФактуры.ФактураСсылка.ПолучитьОбъект();
	СтруктураПоиска = Новый Структура();
	СтруктураПоиска.Вставить("ДокументОснование", Ссылка);
	СтруктураПоиска.Вставить("ЭтапОплаты", 0);
	
	МассивСтрокПоиска = ФактураОбъект.Транзакции.НайтиСтроки(СтруктураПоиска);
	Для Каждого ЭлементМассива Из МассивСтрокПоиска Цикл
		ФактураОбъект.Транзакции.Удалить(ЭлементМассива);	
	КонецЦикла;	
	
	Для Каждого СтрокаТЧ Из Транзакции Цикл
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("ДатаТранзакции", СтрокаТЧ.ДатаТранзакции);
		СтруктураПоиска.Вставить("Карта", СтрокаТЧ.Карта);
		СтруктураПоиска.Вставить("Клиент", СтрокаТЧ.Клиент);
		СтруктураПоиска.Вставить("Автомобиль", СтрокаТЧ.Автомобиль);
		СтруктураПоиска.Вставить("Количество", СтрокаТЧ.Количество);
		СтруктураПоиска.Вставить("Цена", СтрокаТЧ.Цена);

		МассивПоиска = ФактураОбъект.Транзакции.НайтиСтроки(СтруктураПоиска);
		Если МассивПоиска.Количество() > 0 Тогда
				//строка уже оплачена. Удалять и изменять ее не будем
			Продолжить;
		КонецЕсли;

		НоваяСтрокаФактуры = ФактураОбъект.Транзакции.Добавить();
		НоваяСтрокаФактуры.ДатаТранзакции = СтрокаТЧ.ДатаТранзакции;
		НоваяСтрокаФактуры.Карта = СтрокаТЧ.Карта;
		НоваяСтрокаФактуры.Клиент = СтрокаТЧ.Клиент;
		НоваяСтрокаФактуры.Автомобиль       = СтрокаТЧ.Автомобиль;
		НоваяСтрокаФактуры.МестоТранзакции  = СтрокаТЧ.МестоТранзакции;
		НоваяСтрокаФактуры.Продукт          = СтрокаТЧ.Продукт;
		НоваяСтрокаФактуры.Количество       = СтрокаТЧ.Количество;
		НоваяСтрокаФактуры.Цена             = СтрокаТЧ.Цена;
		НоваяСтрокаФактуры.Валюта           = СтрокаТЧ.Валюта;
		НоваяСтрокаФактуры.ВалютаПоставщика = ВалютаПоставщика;
		НоваяСтрокаФактуры.Поставщик        = Поставщик;
		НоваяСтрокаФактуры.ДокументОснование = Ссылка;
		Если ВалютаПоставщика = СтруктураНастроек.BYN Тогда
			СтруктураКурса = Справочники.Валюты.ПолучитьСруктуруКурса(ВалютаПоставщика, СтрокаТЧ.ДатаТранзакции);
			Если СтрокаТЧ.Валюта = СтруктураНастроек.EUR Тогда
				КурсБР 					= СтруктураКурса.КурсКЕвро;
			ИначеЕсли СтрокаТЧ.Валюта = СтруктураНастроек.USD Тогда
				КурсБР 					= СтруктураКурса.КурсКДоллару;
			Иначе
				КурсБР 					= СтруктураКурса.КурсКЕвро;
			КонецЕсли;
			НоваяСтрокаФактуры.ВалютнаяЦена 		= СтрокаТЧ.Цена / КурсБР;
			НоваяСтрокаФактуры.ВалютнаяЦенаСНадбавкой = НоваяСтрокаФактуры.ВалютнаяЦена ;
			НоваяСтрокаФактуры.Сумма = НоваяСтрокаФактуры.ВалютнаяЦенаСНадбавкой * НоваяСтрокаФактуры.Количество;
			НоваяСтрокаФактуры.ЦенаСоСкидкой = НоваяСтрокаФактуры.ВалютнаяЦенаСНадбавкой;
			НоваяСтрокаФактуры.Итого  = НоваяСтрокаФактуры.ВалютнаяЦенаСНадбавкой * НоваяСтрокаФактуры.Количество;
		КонецЕсли;
	КонецЦикла;
	ФактураОбъект.Записать();
КонецПроцедуры

// Получить фактуру.
// 
// Возвращаемое значение:
//  Структура - Получить фактуру:
// * Результат - Булево - 
// * ФактураСсылка - ДокументСсылка.Фактура - 
// * Ошибка 
Функция ПолучитьФактуру()
	Структура = Новый Структура;
	Структура.Вставить("Результат", Истина);
	Структура.Вставить("ФактураСсылка", Документы.Фактура.ПустаяСсылка());
	Структура.Вставить("Ошибка");
	
	ФактураСсылка = Документы.Фактура.ПустаяСсылка();
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(Дата));
	Запрос.Текст = "ВЫБРАТЬ
	|	Фактура.Ссылка КАК ФактураСсылка
	|ИЗ
	|	Документ.Фактура КАК Фактура
	|ГДЕ
	|	Фактура.Дата МЕЖДУ &НачалоПериода И &КонецПериода";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Структура.Результат = Ложь;
		Структура.Ошибка = СтрШаблон("За %1 должна быть создана Фактура", Формат(Дата, "ДФ='MMMM yyyy'"));
		Возврат Структура;
	КонецЕсли;
	Выборка = Результат.Выбрать();
	Если Выборка.Количество() <> 1 Тогда
		Структура.Результат = Ложь;
		Структура.Ошибка = СтрШаблон("За %1 должна быть только одна Фактура", Формат(Дата, "ДФ='MMMM yyyy'"));
		Возврат Структура;
	КонецЕсли;
	Выборка.Следующий();
	Структура.ФактураСсылка = Выборка.ФактураСсылка;
	
	Возврат Структура;		
КонецФункции	
