

#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Период.ДатаНачала = НачалоМесяца(ТекущаяДатаСеанса());
	Период.ДатаОкончания = КонецМесяца(ТекущаяДатаСеанса());
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура СформироватьРеестр(Команда)
	СформироватьРеестрНаСервере();
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьРеестрНаСервере()
	ТабличныйДокумент.Очистить();
	
	Макет = Обработки.РеестрФактур.ПолучитьМакет("РеестрФактур");
	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок"); 
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьИтог = Макет.ПолучитьОбласть("Итог");	
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(Период.ДатаОкончания));
	#Область ТекстЗапроса
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ФактураТранзакции.Клиент КАК Клиент,
		|	ФактураТранзакции.ЭтапОплаты,
		|	ФактураТранзакции.Валюта,
		|	СУММА(ФактураТранзакции.Количество) КАК Количество,
		|	СУММА(ФактураТранзакции.Итого) КАК Итого,
		|	ФактураТранзакции.Ссылка КАК ФактураСсылка,
		|	ФактураТранзакции.Ссылка.Дата КАК ДатаФактуры
		|ПОМЕСТИТЬ ВТ_ДанныеТранзакций
		|ИЗ
		|	Документ.Фактура.Транзакции КАК ФактураТранзакции
		|ГДЕ
		|	ФактураТранзакции.Ссылка.Проведен
		|	И ФактураТранзакции.Ссылка.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|СГРУППИРОВАТЬ ПО
		|	ФактураТранзакции.Клиент,
		|	ФактураТранзакции.Валюта,
		|	ФактураТранзакции.ЭтапОплаты,
		|	ФактураТранзакции.Ссылка,
		|	ФактураТранзакции.Ссылка.Дата
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Клиент,
		|	ФактураСсылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ДанныеТранзакций.Клиент КАК Клиент,
		|	ВТ_ДанныеТранзакций.ЭтапОплаты КАК ЭтапОплаты,
		|	ВТ_ДанныеТранзакций.Валюта КАК Валюта,
		|	ВТ_ДанныеТранзакций.Количество,
		|	ОКР(ВТ_ДанныеТранзакций.Итого, 0) КАК Итого,
		|	ВТ_ДанныеТранзакций.ФактураСсылка,
		|	ВТ_ДанныеТранзакций.ДатаФактуры,
		|	ЕСТЬNULL(НомераФактур.НомерФактуры, 0) КАК НомерФактуры
		|ИЗ
		|	ВТ_ДанныеТранзакций КАК ВТ_ДанныеТранзакций
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НомераФактур КАК НомераФактур
		|		ПО ВТ_ДанныеТранзакций.ФактураСсылка = НомераФактур.Фактура
		|		И ВТ_ДанныеТранзакций.Клиент = НомераФактур.Контрагент
		|
		|УПОРЯДОЧИТЬ ПО
		|	Клиент,
		|	ЭтапОплаты,
		|	Валюта";
	#КонецОбласти	
	
	ОбластьШапка.Параметры.ДатаНач = Формат(Период.ДатаНачала, "ДФ=dd.MM.yyyy;");
	ОбластьШапка.Параметры.ДатаКон = Формат(Период.ДатаОкончания, "ДФ=dd.MM.yyyy;");
	
	//@skip-check new-color
	ЦветФона = Новый Цвет(160, 224, 224);
	
	ТабличныйДокумент.Вывести(ОбластьШапка);
	ОбластьОформления = ТабличныйДокумент.Вывести(ОбластьЗаголовок);
			
	ТабличныйДокумент.Область(ОбластьОформления.Верх, 2, ОбластьОформления.Низ, ТабличныйДокумент.ШиринаТаблицы).ЦветФона = ЦветФона;				
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ИтогКоличество = 0;
	ИтогСумма = 0;
	
	Счетчик = 0;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Счетчик = Счетчик + 1;
		ЗаполнитьЗначенияСвойств(ОбластьСтрока.Параметры, ВыборкаДетальныеЗаписи);
		ОбластьОформления = ТабличныйДокумент.Вывести(ОбластьСтрока);
		
		Если Счетчик % 2 = 0 Тогда
			ТабличныйДокумент.Область(
					ОбластьОформления.Верх, 2, ОбластьОформления.Низ, ТабличныйДокумент.ШиринаТаблицы).ЦветФона = ЦветФона;
		КонецЕсли;
		
		ИтогКоличество = ИтогКоличество + ВыборкаДетальныеЗаписи.Количество;	
		ИтогСумма = ИтогСумма + ВыборкаДетальныеЗаписи.Итого;
	КонецЦикла;	
	
	ОбластьИтог.Параметры.ИтогКоличество = ИтогКоличество;
	ОбластьИтог.Параметры.ИтогИтог = ИтогСумма;
		
	ОбластьОформления = ТабличныйДокумент.Вывести(ОбластьИтог);
	Если Счетчик = 2 = 0 Тогда
		ТабличныйДокумент.Область(
				ОбластьОформления.Верх, 2, ОбластьОформления.Низ, ТабличныйДокумент.ШиринаТаблицы).ЦветФона = ЦветФона;
	КонецЕсли;	
	
	ТабличныйДокумент.ФиксацияСверху = 10;
	ТабличныйДокумент.АвтоМасштаб = Истина;	
КонецПроцедуры
#КонецОбласти

