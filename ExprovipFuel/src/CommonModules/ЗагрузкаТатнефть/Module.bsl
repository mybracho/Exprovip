// Получить структуру загрузки.
// 
// Параметры:
//  СтруктураПараметрыЗагрузки - Структура:
// * АдресВоВременномХранилище - Строка
// * ПапкаЗаправок - СправочникСсылка.МестаТранзакции 
// * РеквизитПоискаНоменклатуры - Строка
// * РасширениеФайла - Строка
// 
// Возвращаемое значение:
//  Структура - Получить структуру загрузки:
// * Результат - Булево - 
// * МассивОшибок - Массив - 
// * МассивЗаправкиНеНашли - Массив - 
// * МассивСтрок - Массив из См. ЗагрузкаОбщий.СтруктураСтрокиЭксельДляЗагрузки()  
Функция ПолучитьСтруктуруЗагрузки(СтруктураПараметрыЗагрузки) Экспорт
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Результат", Истина);
	СтруктураРезультат.Вставить("МассивОшибок", Новый Массив);
	СтруктураРезультат.Вставить("МассивЗаправкиНеНашли", Новый Массив);
	СтруктураРезультат.Вставить("МассивСтрок", Новый Массив);
	
	ВременныйФайлЭксель = ПолучитьИмяВременногоФайла(СтруктураПараметрыЗагрузки.РасширениеФайла);
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(СтруктураПараметрыЗагрузки.АдресВоВременномХранилище); //ДвоичныеДанные
	ДвоичныеДанные.Записать(ВременныйФайлЭксель);
	
	Попытка
		Эксель = Новый COMОбъект("Excel.Application");
		Эксель.DisplayAlerts = 0;
		Эксель.Visible = 0;
	Исключение
		СтруктураРезультат.Результат = Ложь;
		СтруктураРезультат.МассивОшибок = ОписаниеОшибки();
   		Возврат СтруктураРезультат;
	КонецПопытки;
	
	СоответствиеКарточек = ЗагрузкаОбщий.СоответствиеКарточек();
	СоответствиеМестТранзакций = ЗагрузкаОбщий.СоответствиеМестТранзакций(СтруктураПараметрыЗагрузки.ПапкаЗаправок);
	СоответствиеНоменклатуры = ЗагрузкаОбщий.СоответсвиеНоменклатуры(СтруктураПараметрыЗагрузки.РеквизитПоискаНоменклатуры);
	
	Эксель.Workbooks.Open(ВременныйФайлЭксель);	
	Лист = Эксель.Sheets(1);	
	
	СчетчикПустых = 0;
	МассивКарточек = Новый Массив;
	Для Сч = 12 По 100000 Цикл
		Если СчетчикПустых = 1000 тогда
			Прервать;
		КонецЕсли;
		ТекущееЗначениеКарты = Лист.Cells(Сч, 6).Value; //Номера Карт и даты 
		
		ДатаТранзакции = СтрокуВДату(Лист.Cells(Сч,4).Value);	
		
		НомерКарточки = СокрЛП(ТекущееЗначениеКарты);
		НомерКарточки = СтрЗаменить(НомерКарточки, Символы.НПП, "");
		Если ТипЗнч(ДатаТранзакции) <> Тип("Дата") Тогда
			СчетчикПустых = СчетчикПустых + 1;
			Продолжить;
		Иначе
			СчетчикПустых = 0;
		КонецЕсли;		
		
		ЭмитентАЗС        = Лист.Cells(Сч,2).Value;		
		КодЗаправки 	  = СокрЛП(ЭмитентАЗС);		
		
		ПродуктСтрокой = СокрЛП(Лист.Cells(Сч,7).Value);
						                                             
		Если Найти(ПродуктСтрокой,"ДТ")> 0 Тогда
			КолВо  = Окр(Лист.Cells(Сч,8).Value, 2);
			Цена  = Окр(Лист.Cells(Сч,11).Value, 2);
		Иначе   
			КолВо  = Окр(Лист.Cells(Сч,8).Value, 2);
			Цена  = -Окр(Лист.Cells(Сч,11).Value, 2);
		КонецЕсли;
		 
		Если ЗначениеЗаполнено(ДатаТранзакции) Тогда		
									
			СтруктураСтроки = ЗагрузкаОбщий.СтруктураСтрокиЭксельДляЗагрузки();
			СтруктураСтроки.КартаСтрока = НомерКарточки;
			СтруктураСтроки.Карта = СоответствиеКарточек.Получить(НомерКарточки);
			СтруктураСтроки.ДатаТранзакции = ДатаТранзакции;
			СтруктураСтроки.Количество = КолВо;
			СтруктураСтроки.ПродуктСтрока = ПродуктСтрокой;
			СтруктураСтроки.Продукт = СоответствиеНоменклатуры.Получить(ПродуктСтрокой);
			СтруктураСтроки.Цена = Цена;
			СтруктураСтроки.Сумма = Цена * КолВо;
			СтруктураСтроки.МестоТранзакцииСтрока = КодЗаправки;
			СтруктураСтроки.МестоТранзакции = СоответствиеМестТранзакций.Получить(КодЗаправки);
			
			МассивКарточек.Добавить(СтруктураСтроки.Карта);
	
			СтруктураРезультат.МассивСтрок.Добавить(СтруктураСтроки);		
			
		КонецЕсли;	
	КонецЦикла;
	Эксель.ActiveWorkbook.Close(Строка(0));
	Эксель.Quit();
	
	СоответствиеВладельцевКарт = ЗагрузкаОбщий.СоответствиеВладельцевКарт(МассивКарточек);
	СоответствиеАвтомобилейКарт = ЗагрузкаОбщий.СоответствиеАвтомобилейКарт(МассивКарточек);
	Для Каждого СтруктураМассива Из СтруктураРезультат.МассивСтрок Цикл
		СтруктураМассива.Клиент = СоответствиеВладельцевКарт.Получить(СтруктураМассива.Карта);
		СтруктураМассива.Автомобиль = СоответствиеАвтомобилейКарт.Получить(СтруктураМассива.Карта);		
	КонецЦикла;	
	
	Возврат СтруктураРезультат;
КонецФункции

// Строку в дату.
// 
// Параметры:
//  ДатаКакСтрока Дата как строка
// 
// Возвращаемое значение:
//  Дата, Неопределено - Строку в дату
Функция СтрокуВДату(ДатаКакСтрока)
	Результат = Неопределено;
	Попытка
		Массив = СтрРазделить(ДатаКакСтрока, ".", Ложь);
		День = Массив[0];
		Месяц = Массив[1];
		Год = Массив[2];
		
		ДатаСтрокой = СтрШаблон("%1%2%3", Год, Месяц, День);
		Результат = Дата(ДатаСтрокой);
	Исключение
	КонецПопытки;
	Возврат Результат;			
КонецФункции		